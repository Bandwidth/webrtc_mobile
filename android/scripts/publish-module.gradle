apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Bandwidth Android SDK'
                    url = 'https://github.com/Bandwidth/bandwidth-android'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/Bandwidth/bandwidth-android/blob/main/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'hamermike'
                            name = 'Mike Hamer'
                            email = 'mhamer@bandwidth.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/Bandwidth/bandwidth-android.git'
                        developerConnection = 'scm:git:ssh://github.com/Bandwidth/bandwidth-android.git'
                        url = 'https://github.com/Bandwidth/bandwidth-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
